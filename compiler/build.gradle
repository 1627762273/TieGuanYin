import org.gradle.internal.jvm.Jvm

apply plugin: 'java'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':runtime')
    implementation 'com.google.auto.service:auto-service:1.0-rc4'
    implementation 'com.google.auto:auto-common:0.8'
    implementation 'com.squareup:javapoet:1.9.0'
    //implementation 'com.squareup:kotlinpoet:0.6.0'
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compileOnly files(Jvm.current().getToolsJar())
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

//region jitpack
apply plugin: 'maven'
group='com.github.enbandari'

jar {
    from {
        fileTree(dir: 'libs', include: ['*.jar']).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}
//endregion