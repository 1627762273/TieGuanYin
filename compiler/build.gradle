import org.gradle.internal.jvm.Jvm

apply plugin: 'java'
apply plugin: 'kotlin'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':annotations')
    implementation project(':aptutils')
    implementation 'com.google.auto:auto-common:0.8'
    implementation 'com.squareup:javapoet:1.9.0'
    implementation 'com.squareup:kotlinpoet:1.0.0-RC1'
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    compileOnly files(Jvm.current().getToolsJar())
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

jar {
    from {
        fileTree(dir: 'libs', include: ['*.jar']).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

javadoc {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

//region bintray
apply plugin: 'com.novoda.bintray-release'

publish {
    userOrg = project.userName
    groupId = project.groupId
    repoName = project.repoName
    artifactId = 'tieguanyin-compiler'
    publishVersion = project.publishVersion
    desc = project.desc//描述，不重要
    website = project.website
    bintrayUser = project.userName
    bintrayKey = project.apiKey
    dryRun = project.dryRun
}
//endregion